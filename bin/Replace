#! /usr/bin/env perl
#
#    This file is part of ScriptCooker.
#
#    ScriptCooker is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#
#    ScriptCooker is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with ScriptCooker.  If not, see <http://www.gnu.org/licenses/>.
#
# --------------------------------------------------------------
# BV Associates, 2011
#
# CLES :     I-TOOLS
# REVISION : $Revision: 395 $
# AUTEUR :   V. Bauchart (BV Associates)
# DATE :     27/09/2011
#
# --------------------------------------------------------------
#
# OBJECTIF
#    
#
# SYNOPSIS
#    Usage : Replace [-h] [-x] INTO <Table>
#
# OPTIONS
#    -h : l'utilisation de la ligne de commande
#    -x : declenche les triggers
#
# ARGUMENTS
#    Table : Table à modifier
#    Values|- : valeurs séparées par le séparateur de la définition
#    Condition : Selection de ligne pour les tables sans clef
#
# PREREQUIS
#    TOOLS_HOME
#
# REMARQUE
#
# --------------------------------------------------------------
#
# HISTORIQUE
#
# --------------------------------------------------------------

# Inclusions obligatoires
use strict;
use warnings;
use Getopt::Std;
use ScriptCooker::Log 2.1.0 qw{$logger _t set_exit_code};
use ScriptCooker::ITable 2.1.0;
use ScriptCooker::Utils 2.1.0;
use Carp;

# variables globales

sub usage {
    die(_t("Usage: [_1]",<<USAGE ));
Replace [-h] [-x] INTO <Table>
   VALUES <Values|-> [WHERE <Condition> ]
USAGE
}

#  Traitement des Options
###########################################################

my %opts;
getopts( 'hx', \%opts ) or usage();

usage() if $opts{h};


my $execute_trigger = $opts{x};

#  Traitement des arguments
###########################################################

if ( @ARGV < 4 ) {
    $logger->error(_t("Le nombre d'arguments est incorrect"));
    usage();
}

my $table_name;
my @values;
my @conditions;

my $current_keyword;
foreach my $arg ( @ARGV ) {

    if ( ! $current_keyword ) {
        if ( uc($arg) eq 'INTO' ) {
            $current_keyword = uc($arg);
        }
        else {
            usage();
        }
    }
    elsif ($current_keyword eq 'INTO') {
        if ( uc($arg) eq 'VALUES' ) {
            $current_keyword = uc($arg);
        }
        elsif ( ! $table_name) {
            $table_name = $arg;   
        }
        else {
            usage();
        }
    }
    elsif ($current_keyword eq 'VALUES') {
        if ( uc($arg) eq 'WHERE' ) {
            $current_keyword = uc($arg);
        }
        else {
            push @values, $arg;   
        }
    }
    elsif ( $current_keyword eq 'WHERE') {
        push @conditions, $arg;   
    }
}

my $conditions;
if ( @conditions ) {
    $conditions = join(' ', @conditions);
}

if ( ! $table_name ) {
    usage();
}

#use Data::Dumper;die Dumper($table_name,\@values,\@conditions);

#  Corps du script
###########################################################

my $itable;

$itable = ScriptCooker::ITable->open($table_name);

my @columns = $itable->field();

my $separator = $itable->definition()->separator();

if ( ! $itable->key() && ! $conditions ) {
    die(_t("Impossible de remplacer une ligne sur une table sans clef"));
}

if ( $conditions ) {
    $itable->query_condition($conditions);
}

# recherche des VALUES sur l'entree standard
if ( @values == 1 && $values[0] eq '-' ) {
    my $values = <STDIN>;
    chomp $values;

    if ( $values ) {
        @values = ( $values );
    }
    else {
        @values = ();
    }
}

# découpe les valeurs
my @split_values;
foreach my $value ( @values ) {
    # essayons de decouper les lignes
    my @field = split($separator, $value,-1);
    push @split_values, @field;
}
@values=@split_values;

if ( @values > @columns ) {
    die (_t("Il y a trop de valeurs par rapport au nombre de colonnes"));
}
if ( @values < @columns ) {
    die (_t("Il n'y a pas assez de valeurs par rapport au nombre de colonnes"));
}

my %update_hash;
foreach my $field ( @columns ) {
    $update_hash{$field}=shift @values;
}

if ( $execute_trigger ) {
    local %ENV = ( %ENV, %update_hash );
    eval { $itable->eval_trigger('Replace','PreAction') };
    if ( $@ ) {
        set_exit_code(202);
        warn($@);
    }
}

# mise à jour effective
$itable->update_row(%update_hash);

if ( $execute_trigger ) {
    local %ENV = ( %ENV, %update_hash );
    eval { $itable->eval_trigger('Replace','PostAction') };
    if ( $@ ) {
        set_exit_code(202);
        warn($@);
    }
}

